plugins {
	id 'fabric-loom' version '1.10-SNAPSHOT'
	id 'maven-publish'
	id "com.modrinth.minotaur" version "2.+"
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	maven {
		name = 'Ladysnake Mods'
		url = 'https://maven.ladysnake.org/releases/'
	}
	maven {
		name = 'NucleoidMC'
		url = 'https://maven.nucleoid.xyz/'
	}
	maven {
		name = "Fuzs Mod Resources"
		url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/"
	}
	maven {
		name = 'Modrinth'
		url = 'https://api.modrinth.com/maven/'
	}
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	modImplementation "maven.modrinth:anshar:1.0.2"
	modImplementation "org.ladysnake.cardinal-components-api:cardinal-components-base:6.1.2"
	modImplementation "org.ladysnake.cardinal-components-api:cardinal-components-block:6.1.2"
	modImplementation "org.ladysnake.cardinal-components-api:cardinal-components-level:6.1.2"
	modImplementation "org.ladysnake.cardinal-components-api:cardinal-components-entity:6.1.2"
	modImplementation "fuzs.forgeconfigapiport:forgeconfigapiport-fabric:21.1.3"

	modImplementation include("eu.pb4:polymer-core:0.9.18+1.21.1")
	modImplementation include("eu.pb4:polymer-virtual-entity:0.9.18+1.21.1")
	modImplementation include("xyz.nucleoid:server-translations-api:2.3.1+1.21-pre2")
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 21
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}" }
	}
}

// Configure the maven publication
publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}

if (System.getenv("MODRINTH")) {
	modrinth {
		token = System.getenv("MODRINTH")
		projectId = 'wHXli9kE'// The ID of your modrinth project, slugs will not work.
		versionNumber = "" + version // The version of the mod to upload.
		versionType = "alpha"
		uploadFile = remapJar // This links to a task that builds your mod jar and sets "uploadFile" to the mod jar.
		gameVersions = [((String) project.minecraft_version)]
		changelog = System.getenv("CHANGELOG")
		loaders = ["fabric", "quilt"]

		dependencies {
			required.project "anshar"
		}
	}

	remapJar {
		finalizedBy project.tasks.modrinth
	}
}